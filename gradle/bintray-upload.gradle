apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    def variants = android.hasProperty('applicationVariants') \
        ? android.applicationVariants \
        : android.libraryVariants

    source = variants.release.javaCompile.source
    classpath = files(variants.release.javaCompile.classpath.files, android.bootClasspath)

    options.links("https://docs.oracle.com/javase/7/docs/api/");
    options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']

    dryRun = project.hasProperty('christophermcasey.dryRun') \
            ? project.property('christophermcasey.dryRun') \
            : false
    publish = project.hasProperty('christophermcasey.publish') \
            ? project.property('christophermcasey.publish') \
            : false

    pkg {
        repo = project.property('christophermcasey.groupId')
        name = project.property('christophermcasey.artifactId')
        userOrg = 'christophermcasey'
        desc = project.property('christophermcasey.desc')
        websiteUrl = "https://github.com/christophermcasey/${project.property('christophermcasey.githubRepo')}"
        vcsUrl = "git://github.com/christophermcasey/${project.property('christophermcasey.githubRepo')}.git"
        issueTrackerUrl = "https://github.com/christophermcasey/${project.property('christophermcasey.githubRepo')}/issues"
        licenses = ['MIT']
        labels = ['android', 'christophermcasey']
        publicDownloadNumbers = project.hasProperty('christophermcasey.publicDownloadNumbers') \
            ? project.property('christophermcasey.publicDownloadNumbers') \
            : false
        version {
            name = project.property('christophermcasey.version')
            vcsTag = project.property('christophermcasey.version')
            released = new Date()
        }
    }
}
